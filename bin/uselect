#!/usr/bin/env perl
use strict;
use warnings;

# PODNAME: uselect
# ABSTRACT: interactive selection filter
# VERSION

use App::USelect qw( run );
use Getopt::Long qw( GetOptions );
use Pod::Usage qw( pod2usage );

my $version = $App::USelect::VERSION || 'DEVELOPMENT';

my %opt = (
    help            => 0,
    include_blanks  => 0,
    message         => "uselect v$version",
    select_code     => '1',
    single_select   => 0,
);

my @options_spec = (
    "blank_lines|b"     => \$opt{include_blanks},
    "help|h|?"          => \$opt{help},
    "message|m=s"       => \$opt{message},
    "select|s=s"        => \$opt{select_code},
    "single|1"          => \$opt{single_select},
    "version|v"         => sub {
            print "uselect v$version",
            exit 0;
        },
);
if (!GetOptions(@options_spec) || $opt{help}) {
    pod2usage( -exitval => 1, -msg => "uselect v$version" );
}

my $err = run(\%opt, \@ARGV);
exit $err;

__END__

=pod

=head1 SYNOPSIS

uselect [options] [select text]

  options:
     --blank_lines, -b
        Force blank lines to be selectable.

    --help, -h, -?
        Show this help.

    --message <message, -m <message>
        Specify a message to display in the status line

    --select <perl expression>, -s <perl expression>
        Select only those lines for which given perl expression evaluates to
        true. The input lines are passed as $_.

    --single, -1
        Single selection mode

    --version, -v
        Print the version and exit.

  Input lines can be specified on the command line after the options, otherwise
  they will be read from stdin.

=head1 DESCRIPTION

uselect is a reimplementation of iselect by Ralf S. Engelschall

    http://www.ossp.org/pkg/tool/iselect/

uselect is intended to be used as an interactive unix filter. Input lines are
displayed to the user, and selected lines are written to stderr.

=head1 SERVING SUGGESTIONS

fv() { vim $( find . -type f | sort | fgrep "$@" | uselect ); }

gv() { vim $( ack --heading --break "$@" | uselect -s '!/^\d+:/ ); }

=cut
