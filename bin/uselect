#!/usr/bin/env perl

use version; our $VERSION = qv('2011.06.28');

use Modern::Perl;
use App::USelect::Selector;
use App::USelect::Display;
use Getopt::Long;
use autodie;

my $include_blanks = 0;
my $select_code = '1';

process_options();

if (not $include_blanks) {
    $select_code = '/./ && (' . $select_code . ')';
}

my $select_func = eval('sub { local $_ = $_[0]; ' . $select_code . '}');
usage($@) if $@;

chomp(my @lines = @ARGV ? @ARGV : <STDIN>);

my $selector = App::USelect::Selector->new(
        text          => \@lines,
        is_selectable => $select_func,
    );
if (not $selector->grep(sub { $_->can_select  })) {
    say STDERR 'No selectable lines.';
    exit 0;
}

my $stdout = attach_console();

my $display = App::USelect::Display->new(selector => $selector);
$display->run;

detach_console($stdout);

say for $selector->selection;

#-------------------------------------------------------------------------------

sub usage {
    say STDERR for @_;
    say STDERR "usage: $0 [options] [items]";
    say STDERR <<'END_USAGE';
options:
    --blank-lines|-b            make blank lines selectable

    --select|-s <perlcode>      only lines matching perlcode are selectable
                                input line is in $_

    --version|-V

END_USAGE

    exit 1;
}

sub attach_console {
    open(STDIN, '<', '/dev/tty');

    open my $stdout, '>&STDOUT';
    open(STDOUT, '>', '/dev/tty');

    return $stdout;
}

sub detach_console {
    my ($stdout) = @_;
    open(STDOUT, '>&', $stdout);
}

sub process_options {
    my @options_spec = (
        "blank_lines|b"     => \$include_blanks,
        "select|s=s"        => \$select_code,
        "version|V"         => sub { say "uselect v$VERSION"; exit 0 },
    );

    if (not GetOptions(@options_spec)) {
        usage();
    }
}
